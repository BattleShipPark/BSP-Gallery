apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        release {
            storeFile file("..\\battleshippark.keystore")
            storePassword System.getProperty("store_pwd")
            keyAlias System.getProperty("key_alias")
            keyPassword System.getProperty("key_pwd")
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    //noinspection GroovyAssignabilityCheck
    defaultConfig {
        applicationId "com.battleshippark.bsp_gallery"
        multiDexEnabled true
        targetSdkVersion 25
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode 2
        versionName "1.1"
    }

    //noinspection GroovyAssignabilityCheck
    buildTypes {
        release {
            manifestPlaceholders = [app_name: 'BSP-Gallery']
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            manifestPlaceholders = [app_name: 'BSP-Gallery.debug']
        }
    }

    productFlavors {
        fast {
            minSdkVersion 21
        }
        compatible {
            minSdkVersion 16
        }
    }
    //noinspection GroovyAssignabilityCheck
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //noinspection GroovyAssignabilityCheck
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        // due to butterknife + lombok
    }

    //noinspection GroovyAssignabilityCheck
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->

        tasks.getByPath(":${project.name}:process${variant.name.capitalize()}GoogleServices").doFirst {
            def appModuleRootFolder = '.'
            def googleServicesJson = 'google-services.json'
            def copyTask = project.tasks.create("_switchGoogleServices", Copy)

            copyTask.from("conf/${buildType.name}")
            copyTask.include("$googleServicesJson")
            copyTask.into("$appModuleRootFolder")
            copyTask.inputs.file "conf/${buildType.name}/${googleServicesJson}"
            copyTask.outputs.file "${appModuleRootFolder}/${googleServicesJson}"
            copyTask.execute();
        }

        tasks.getByPath(":${project.name}:assemble${buildType.name.capitalize()}").doLast {
            def file = output.outputFile
            def copyTask = project.tasks.create("renameApk", Copy)
            copyTask.from(file)
            copyTask.into(file.parent)
            copyTask.rename(file.name, file.name.replace("app-", "bsp-gallery-" + android.defaultConfig.versionName + "-")
                    .replace(".apk", "-" + getDate() + ".apk"))
            copyTask.execute()
        }
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.6.2'
    testCompile 'org.mockito:mockito-core:2.7.1'
    //
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support:support-annotations:25.1.1'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:2.7.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.annimon:stream:1.1.4'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'org.parceler:parceler-api:1.1.6'
    apt 'org.parceler:parceler:1.1.6'
    provided 'org.projectlombok:lombok:1.16.12'
    apt 'org.projectlombok:lombok:1.16.12'
}
