apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'

android {
    signingConfigs {
        release {
            storeFile file("..\\battleshippark.keystore")
            storePassword System.getenv("store_pwd")
            keyAlias System.getenv("key_alias")
            keyPassword System.getenv("key_pwd")
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "com.battleshippark.bsp_gallery"
        minSdkVersion 15
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        // due to butterknife + lombok
    }
    productFlavors {
    }
}

def appModuleRootFolder = '.'
def googleServicesJson = 'google-services.json'

task switchToDebug(type: Copy) {
    def buildType = 'debug'
    description = 'Switches to DEBUG google-services.json'
    from "conf/${buildType}"
    include "$googleServicesJson"
    into "$appModuleRootFolder"
    inputs.file file("conf/${buildType}/${googleServicesJson}")
    outputs.file file("${appModuleRootFolder}/${googleServicesJson}")
}

task switchToRelease(type: Copy) {
    def buildType = 'release'
    description = 'Switches to RELEASE google-services.json'
    from "conf/${buildType}/"
    include "$googleServicesJson"
    into "$appModuleRootFolder"
    inputs.file file("conf/${buildType}/${googleServicesJson}")
    outputs.file file("${appModuleRootFolder}/${googleServicesJson}")
}

afterEvaluate {
    processDebugGoogleServices.dependsOn switchToDebug
    processReleaseGoogleServices.dependsOn switchToRelease
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //
    androidTestCompile 'com.android.support.test:runner:0.4'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:0.4'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.android.gms:play-services-analytics:8.1.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'org.projectlombok:lombok:1.16.6'
}
